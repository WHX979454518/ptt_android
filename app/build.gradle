//apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

project.ext.buildNumber = System.getenv("BUILD_NUMBER") ?: "1000000"
project.ext.hockeyAppId = System.getenv("HOCKEYAPP_ID") ?: "4af1b9ae5acc44629ae26de41a61baa4"


buildscript {
    ext.kotlin_version = '1.0.3'
    ext.support_library_version = '24.0.0'

    repositories {
        // required for this library, don't use mavenCentral()
        jcenter()
        maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

repositories {
    jcenter()

    maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }

    flatDir { dirs 'aars' }
}

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'

    defaultConfig {
        applicationId "com.xianzhitech.ptt"
        minSdkVersion 15
        targetSdkVersion 24
        versionCode 1
        versionName System.getenv("BUILD_TAG") ?: "1.0-snapshot"

        buildConfigField("String", "BUILD_NUMBER", "\"$buildNumber\"")
    }

    signingConfigs {
        release {
            storeFile file('../etc/release.jks')
            storePassword 'TNZ33ACzYPBsgXrQGBb7'
            keyAlias 'android'
            keyPassword 'TNZ33ACzYPBsgXrQGBb7'
        }

        debug {
            storeFile file('../etc/debug.keystore')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        dev.java.srcDirs += 'src/dev/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    productFlavors {
        dev {
            applicationId 'com.xianzhitech.ptt.dev'
            buildConfigField("String", "APP_SERVER_ENDPOINT", '"http://netptt.cn:20002"')
//            buildConfigField("String", "APP_SERVER_ENDPOINT", '"http://cdjiahotx.eicp.net:20002"')
//            buildConfigField("String", "APP_SERVER_ENDPOINT", '"http://192.168.2.2:8000"')
        }

        uat {
            applicationId 'com.xianzhitech.ptt.test'
            buildConfigField("String", "HOCKEYAPP_ID", "\"$hockeyAppId\"")
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    androidTestCompile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'

    uatCompile 'net.hockeyapp.android:HockeySDK:3.7.0'

    devCompile 'com.facebook.stetho:stetho:1.3.1'
    devCompile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    devCompile 'com.hanhuy.android:viewserver:1.0.3'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    devCompile 'com.github.franmontiel:PersistentCookieJar:v0.9.3'

    compile 'com.belerweb:pinyin4j:2.5.1'

    compile(name: 'webrtc-android-release', ext: 'aar')

    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:preference-v7:$support_library_version"
    compile "com.android.support:preference-v14:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"
    compile "com.android.support:percent:$support_library_version"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile ('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    compile ('io.socket:socket.io-client:0.7.0') {
//    compile (project(':socket.io-client-java')) {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'
    compile 'com.trello:rxlifecycle:0.6.1'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.0@aar') {
        transitive = true;
    }
}


