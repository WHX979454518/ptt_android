apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

project.ext.buildNumber = System.getenv('BUILD_NUMBER') ?: 'dev'
project.ext.buildName = "1.6.$buildNumber"
project.ext.appServerUrl = System.getenv('APP_SERVER_URL')

println("Building app version $buildName($buildNumber)")

buildscript {
    repositories {
        // required for this library, don't use mavenCentral()
        jcenter()
        maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {

        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}

repositories {
    jcenter()

    maven { url 'http://oss.sonatype.org/content/repositories/snapshots' }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }

    maven {
        url "https://repo.simophin.net/artifactory/libs-snapshot-local"
        credentials {
            username = "netptt"
            password = "APdaMmwae2hGMieAVa27dyGPUN1cAWaahnbwp"
        }
    }

    flatDir { dirs 'aars' }
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId "com.xianzhitech.ptt.zrt"
        minSdkVersion min_sdk_version
        targetSdkVersion compile_sdk_version
        versionCode 6
        versionName buildName
        multiDexEnabled true

        buildConfigField("String", "BUILD_NUMBER", "\"$buildNumber\"")
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            storeFile file('../etc/release.jks')
            storePassword 'TNZ33ACzYPBsgXrQGBb7'
            keyAlias 'android'
            keyPassword 'TNZ33ACzYPBsgXrQGBb7'
        }

        debug {
            storeFile file('../etc/debug.keystore')
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        dev.java.srcDirs += 'src/dev/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    productFlavors {
        dev {
            def appServerUrl = project.appServerUrl ?: "http://netptt.cn:20002"
            applicationIdSuffix ".dev"
            buildConfigField("String", "APP_SERVER_ENDPOINT", "\"$appServerUrl\"")
            manifestPlaceholders["BD_LOCATION_SDK_KEY"] = "hWdRd0tU6Fkv1BOUteDzp4HsgkUvkNuz"
            println("Using $appServerUrl as app server for dev build")

            buildConfigField("boolean", "IS_PRODUCTION", "false")
        }

        prod {
            def appServerUrl = project.appServerUrl ?: "http://netptt.cn:10005"
            buildConfigField("String", "APP_SERVER_ENDPOINT", "\"$appServerUrl\"")
            manifestPlaceholders["BD_LOCATION_SDK_KEY"] = "M7uMw7magyVuxsUSttGeQ123k10LREz4"
            println("Using $appServerUrl as app server for production build")

            buildConfigField("boolean", "IS_PRODUCTION", "true")
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    androidTestCompile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    compile 'com.android.support:multidex:1.0.1'

    devCompile 'com.github.franmontiel:PersistentCookieJar:v0.9.3'

    compile 'com.jakewharton.threetenabp:threetenabp:1.0.4'

    compile 'com.belerweb:pinyin4j:2.5.1'

    //compile(name: 'webrtc-android-release', ext: 'aar')
    compile(group: 'cn.netptt', name: 'engine', version: '2.0.0-20170328.205927-1', ext: 'aar')
//    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':bd_loc_sdk')

    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:preference-v7:$support_library_version"
    compile "com.android.support:preference-v14:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"
    compile "com.android.support:percent:$support_library_version"

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile ('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    compile ('io.socket:socket.io-client:0.8.2') {
//    compile (project(':socket.io-client-java')) {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    // https://mvnrepository.com/artifact/com.squareup.okhttp3/okhttp-ws
    compile group: 'com.squareup.okhttp3', name: 'okhttp-ws', version: '3.4.1'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.10'
    compile 'io.reactivex:rxkotlin:0.60.0'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'

    compile 'com.github.tony19:logback-android-core:1.1.1-6'
    compile('com.github.tony19:logback-android-classic:1.1.1-6') {
        // workaround issue #73
        exclude group: 'com.google.android', module: 'android'
    }

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-android
//    prodCompile group: 'org.slf4j', name: 'slf4j-android', version: '1.7.21'


    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.1@aar') {
        transitive = true;
    }

    kapt 'com.android.databinding:compiler:2.3.0'
}

kapt {
    generateStubs = true
}


